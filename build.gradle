plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java-library'
    id 'maven-publish'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'jacoco'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id "org.flywaydb.flyway" version "[9.+,10.0["
}

tasks.withType(Jar).configureEach {
    zip64 = true
}

subprojects {
    tasks.withType(Jar).configureEach {
        zip64 = true
    }
}

jacoco {
    toolVersion = '0.8.10'
}

targetCompatibility = JavaVersion.VERSION_21
sourceCompatibility = JavaVersion.VERSION_21

group = "org.codeforamerica.platform"
version = "1.6.43-SNAPSHOT"
ext.admin = System.getenv("SONATYPE_USERNAME")
def localEnv = new Properties()
if (file(".env").isFile()) {
    file(".env").withInputStream { localEnv.load(it) }
}
def sonatypeUsername = localEnv.getProperty("SONATYPE_USERNAME") ?: System.getenv("SONATYPE_USERNAME")
def sonatypePassword = localEnv.getProperty("SONATYPE_PASSWORD") ?: System.getenv("SONATYPE_PASSWORD")

jar {
    zip64 = true
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'formflow.library.ScreenController'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

bootJar {
    enabled = false
}

task webjar(type: Jar) {
    zip64 = true
    from(fileTree("dist")) {
        into "META-INF/resources"
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    api 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'org.springframework.integration:spring-integration-ws:6.5.3'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.jetbrains:annotations:26.0.2-1'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.11.0'
    implementation 'org.flywaydb:flyway-core:11.15.0'
    implementation "org.flywaydb:flyway-database-postgresql:11.15.0"
    implementation 'org.webjars.npm:dropzone:5.9.3'
    implementation 'software.amazon.awssdk:s3:2.36.3'
    implementation 'com.smartystreets.api:smartystreets-java-sdk:4.2.0'
    implementation 'com.github.librepdf:openpdf:3.0.0'
    implementation 'org.yaml:snakeyaml:2.5'
    implementation 'com.mailgun:mailgun-java:2.1.0'
    implementation 'com.google.crypto.tink:tink:1.19.0'
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.apache.tika:tika-core:3.2.3'
    implementation 'commons-codec:commons-codec:1.19.0'
    implementation 'org.apache.commons:commons-text:1.14.0'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.jsoup:jsoup:1.21.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:6.3.2'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.seleniumhq.selenium:selenium-java'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'org.projectlombok:lombok:1.18.42'
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.2.1'
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.82'
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('bootJar').configure {
    enabled = false
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = "form-flow"
            artifact(tasks.named("jar").get().archiveFile)

            artifact tasks.javadocJar
            artifact tasks.sourcesJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "Form Flow Builder"
                description = "A Java library placed in a Spring Boot app to generate form flows."
                url = "https://github.com/codeforamerica/form-flow/"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/git/git-scm.com/main/MIT-LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'birdprsn'
                        name = 'Alex Gonzalez'
                        email = 'agonzalez@codeforamerica.org'
                    }
                    developer {
                        id = 'amedrano'
                        name = 'Ana Medrano'
                        email = 'amedrano@codeforamerica.org'
                    }
                    developer {
                        id = 'mperlman'
                        name = 'Marc Perlman'
                        email = 'mperlman@codeforamerica.org'
                    }
                    developer {
                        id = 'cenyia'
                        name = 'Chibuisi Enyia'
                        email = 'cenyia@codeforamerica.org'
                    }
                    developer {
                        id = 'mtodd'
                        name = 'Morgan Todd'
                        email = 'mtodd@codeforamerica.org'
                    }
                }
                scm {
                    url = 'scm:git:https://github.com/codeforamerica/form-flow.git'
                }
            }
        }
    }
}

signing {
    required { admin }
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype {
            if (version.endsWith("-SNAPSHOT")) {
                println("Using a SNAPSHOT: ${version}")
            } else {
                println("Using a release version: ${version}")
            }
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

javadoc {
    options.addStringOption('Xmaxwarns', '1000')
}
